
/*
 * In order to use ml-gradle, it must be applied. This occurs after the two above blocks for overriding properties so
 * that those blocks can override properties before ml-gradle initializes itself.
 */
plugins {
  id "com.marklogic.ml-gradle" version "2.0rc3" 
  id "java"
  id "eclipse"
  id "application"
  id "wrapper"
}

/*
 * Everything below this line is optional and is intended to show different features provided by ml-gradle.
 */

/*
 * This lists the repositories for finding project dependencies (which differ from buildscript dependencies - buildscript
 * dependencies are not included in project artifacts that are built). These repositories are needed to satisfy the
 * dependencies expressed below.
 */
repositories {
    jcenter()
    
    // Needed for mlcp dependencies
    maven {url "http://developer.marklogic.com/maven2/"}
    
    // Stores the corb jar - this will be moved to bintray soon
    maven {url "http://rjrudin.github.io/marklogic-java/releases"}
    
    // Needed for hadoop dependencies for mlcp
    maven {url "http://repository.cloudera.com/artifactory/cloudera-repos/" }
}

configurations {
    // This configuration captures the dependencies for running mlcp (Content Pump). This is only needed if you want
    // to run mlcp via Gradle tasks. If you do, using com.marklogic.gradle.task.MlcpTask is a useful starting point, as
    // shown below.
    mlcp
}

/*
 * This expresses dependencies on the ml-java and ml-junit libraries. The former is typically used for application
 * code, and the latter supports writing JUnit-based tests. A dependency exists on RestAssured to show an option for
 * using that library in tests that must invoke MarkLogic HTTP endpoints.
 */
dependencies {
    // Needed to compile and run the JUnit tests
    testCompile "com.marklogic:ml-junit:2.5"
    testCompile "com.jayway.restassured:rest-assured:2.4.1"

    compile "org.springframework:spring-context:4.2.4.RELEASE"
    
    mlcp "com.marklogic:mlcp-Hadoop2:1.3-2"
    mlcp "com.marklogic:marklogic-mapreduce2:2.1.3"
}

task importPresentationMlTemplate(type: com.marklogic.gradle.task.MlcpTask) {
    classpath = configurations.mlcp
    command = "IMPORT"
    database = mlAppConfig.contentDatabaseName
    document_type = "mixed"
    input_file_type = "documents"
    input_file_path = "src/main/resources/powerpoint"
    input_compressed = "true"
    output_collections = "presentationML"
    output_permissions = "rest-reader,read,rest-writer,update"
    output_uri_replace = ".*powerpoint/,'/'"
    args = ["-input_compression_codec", "zip"]
}

task importSampleData(type: com.marklogic.gradle.task.MlcpTask) {
    classpath = configurations.mlcp
    command = "IMPORT"
    database = mlAppConfig.contentDatabaseName
    input_file_path = "src/main/resources/sample"
    output_collections = "sample"
    output_permissions = "rest-reader,read,rest-writer,update"
    output_uri_replace = ".*sample/,'/'"
}


mainClassName = "com.marklogic.presentationml.Application"